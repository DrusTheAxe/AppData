@ECHO Off
SETLOCAL

IF %1x == x GoTo Help

SET VER=%1

SET ROOTDIR=%~dp0
SET BINDIR=%ROOTDIR%bin\Release\AnyCPU
SET TARGETDIR=%ROOTDIR%Release
SET TARGETBASE=%TARGETDIR%\AppData-%VER%

IF "%ZIPEXE%" == "" SET ZIPEXE=C:\Util.W32\ZIP.EXE
SET ZOPTS=-9jDX
SET ZTARGET=%TARGETBASE%.zip
SET FILES="%BINDIR%\*" "%ROOTDIR%LICENSE" "%ROOTDIR%README.html" "%ROOTDIR%README.md"

IF "%_7ZDIR%" == "" SET _7ZDIR=C:\Util.W32\7-ZipPortable\App
IF "%_7Zx86%" == "" SET _7Zx86=%_7ZDIR%\7-Zip\7z.exe
IF "%_7Zx64%" == "" SET _7Zx64=%_7ZDIR%\7-Zip64\7z.exe
SET _7ZEXE=%_7Zx86%
IF %PROCESSOR_ARCHITECTURE% == AMD64 SET _7ZEXE=%_7Zx64%
SET _7ZOPTS=-mx=9 -myx=9 -sse
SET _7ZTARGET=%TARGETBASE%.7z

IF NOT EXIST %TARGETDIR% MD %TARGETDIR% 2>&1 >NUL
IF EXIST %ZTARGET% DEL /Q %ZTARGET%
ECHO %ZIPEXE% %ZOPTS% %ZTARGET% %FILES%
%ZIPEXE% %ZOPTS% %ZTARGET% %FILES%
IF ERRORLEVEL 1 GOTO TheEnd

IF EXIST %_7ZTARGET% DEL /Q %_7ZTARGET%
ECHO %_7ZEXE% a %_7ZOPTS% %_7ZTARGET% %FILES%
%_7ZEXE% a %_7ZOPTS% %_7ZTARGET% %FILES%
IF ERRORLEVEL 1 GOTO TheEnd

GoTo TheEnd

:Help
ECHO Usage: MAKEZIP version

:TheEnd
ENDLOCAL
